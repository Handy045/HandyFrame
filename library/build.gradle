apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                // 注意此处有顺序要求
                arguments = [targetModuleName: getModuleName().replace("\"", "\'")]
                arguments = [eventBusIndex: 'com.handy.common.MyEventBusIndex']
                arguments = [AROUTER_MODULE_NAME: getModuleName().replace("\"", "\'")]
            }
        }

        ndk {
            // 设置支持的SO库架构
            abiFilters 'arm64-v8a', 'armeabi', 'armeabi-v7a', 'x86', 'x86_64'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false  // 混淆
            zipAlignEnabled true    // Zipalign优化
            shrinkResources false    // 移除无用的resource文件
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false  // 混淆
            zipAlignEnabled true    // Zipalign优化
            shrinkResources false    // 移除无用的resource文件
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        android.libraryVariants.all { variant ->
            variant.outputs.all {
                if (outputFileName.endsWith('.aar')) {
                    if (variant.buildType.name == 'debug') {
                        outputFileName = "${getModuleName()}_V${defaultConfig.versionName}_${releaseTime()}_Debug.aar"
                    } else if (variant.buildType.name == 'release') {
                        outputFileName = "${getModuleName()}_V${defaultConfig.versionName}_${releaseTime()}_Release.aar"
                    }
                }
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            assets.srcDirs = ['src/main/assets/']
            res.srcDirs = ['src/main/res/', 'src/main/res-theme/']
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //==============================
    //          官方支持库
    //==============================
    api 'com.android.support:design:28.0.0'
    api 'com.android.support:support-v4:28.0.0'
    api 'com.android.support:appcompat-v7:28.0.0'

    //==============================
    //          测试库
    //==============================
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //==============================
    //          基础库
    //==============================
    // TODO HandyBase - https://github.com/Handy045/HandyBasic
    // TODO 已依赖：AndroidUtilCode、Bugly、EventBus、DBFlow、BGASwipeBackLayout
    api 'com.github.Handy045:HandyBasic:1.0.0'
    annotationProcessor 'com.github.Raizlabs.DBFlow:dbflow-processor:4.2.4'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'

    //==============================
    //          框架库
    //==============================
    // TODO Butterknife - https://github.com/JakeWharton/butterknife
    api 'com.jakewharton:butterknife:9.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0'
    // TODO ArouterApi - https://github.com/alibaba/ARouter
    api 'com.alibaba:arouter-api:1.4.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'

    //==============================
    //          功能库
    //==============================
    // TODO LuBan - https://github.com/Curzibn/Luban
    api 'top.zibin:Luban:1.1.8'
    // TODO Glide - https://github.com/bumptech/glide
    api 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    // TODO Easing - https://github.com/daimajia/AnimationEasingFunctions
    api 'com.daimajia.easing:library:2.0@aar'
    // TODO AndroidAnimations - https://github.com/daimajia/AndroidViewAnimations
    api 'com.daimajia.androidanimations:library:2.3@aar'
    // TODO DebugDB - https://github.com/amitshekhariitbhu/Android-Debug-Database
    api 'com.amitshekhar.android:debug-db:1.0.6'
    // TODO HandyQRCode - https://github.com/Handy045/HandyQRCode
    api 'com.github.Handy045:HandyQRCode:1.1.3'
    // TODO HandyKeepAlive - https://github.com/Handy045/HandyKeepAlive
    api 'com.github.Handy045:HandyKeepAlive:1.0.1'

    //==============================
    //          控件库
    //==============================
    // TODO HandyTitleBar - https://github.com/Handy045/HandyTitleBar
    api 'com.github.Handy045:HandyTitleBar:1.4.0'
    // TODO Album - https://github.com/yanzhenjie/Album
    api 'com.yanzhenjie:album:2.1.3'
    // TODO Banner - https://github.com/bingoogolapple/BGABanner-Android
    api 'cn.bingoogolapple:bga-banner:2.2.6'
    // TODO Material - https://github.com/rey5137/material
    api 'com.github.rey5137:material:1.2.5'
    // TODO DialogUI - https://github.com/jjdxmashl/jjdxm_dialogui
    api 'com.dou361.dialogui:jjdxm-dialogui:1.0.3'
    // TODO SmartRefreshLayout - https://github.com/scwang90/SmartRefreshLayout
    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    api 'com.scwang.smartrefresh:SmartRefreshHeader:1.0.5.1'
    // TODO PickerView - https://github.com/Bigkoo/Android-PickerView
    api 'com.contrarywind:Android-PickerView:4.1.7'
    // TODO SweetAlertDialog - https://github.com/Handy045/JointSweetAlertDialog
    api 'com.github.cazaea:sweet-alert-dialog:1.0.0'
}

/**
 * 获取当前Module名称
 */
static String getModuleName() {
    return "HandyFrame"
}

/**
 * 获取当前系统时间
 */
static def releaseTime() {
    String data = new Date().format("yyyy-MM-dd HH:mm", TimeZone.getTimeZone("GMT+08:00"))
    data = data.replace(":", "")
    data = data.replace("-", "")
    data = data.replace(" ", "")
    data = data.substring(2)
    return data
}

//-------------------- jitPack生成文档 --------------------
tasks.withType(JavaCompile) { //指定编码
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar) { //打包源码
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) { //制作文档(Javadoc)
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
